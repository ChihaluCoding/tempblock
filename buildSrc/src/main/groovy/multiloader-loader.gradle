import org.gradle.api.file.DuplicatesStrategy

plugins {
    id 'multiloader-common'
}

configurations {
    commonJava{
        canBeResolved = true
    }
    commonResources{
        canBeResolved = true
    }
}

dependencies {
    compileOnly(project(':common')) {
        capabilities {
            requireCapability "$group:$mod_id"
        }
    }
    commonJava project(path: ':common', configuration: 'commonJava')
    commonResources project(path: ':common', configuration: 'commonResources')
}

def commonProject = project(':common')
def commonSourceSets = commonProject.extensions.getByName('sourceSets')
sourceSets {
    main {
        commonSourceSets.main.java.srcDirs.each { srcDir -> java.srcDir(srcDir) }
        commonSourceSets.main.resources.srcDirs.each { srcDir -> resources.srcDir(srcDir) }
    }
}

tasks.named('compileJava', JavaCompile) {
    dependsOn(configurations.commonJava)
    source(configurations.commonJava)
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
}

tasks.named('javadoc', Javadoc).configure {
    dependsOn(configurations.commonJava)
    source(configurations.commonJava)
}

tasks.named('sourcesJar', Jar) {
    dependsOn(configurations.commonJava)
    from(configurations.commonJava)
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
